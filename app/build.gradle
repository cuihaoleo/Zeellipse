import org.apache.tools.ant.taskdefs.condition.Os

// NDK auto detect code, from http://stackoverflow.com/questions/28222158
def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    def ndkdir = properties.getProperty('ndk.dir', null)
    if (ndkdir == null)
        throw new GradleException("NDK location not found. Define location with ndk.dir in the" /
                      "local.properties file or with an ANDROID_NDK_ROOT environment variable.")

    return ndkdir
}

def getNdkBuildCmd() {
    def ndkbuild = getNdkDir() + "/ndk-build"
    if (Os.isFamily(Os.FAMILY_WINDOWS))
        ndkbuild += ".cmd"

    return ndkbuild
}

apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"
    sourceSets.main.jni.srcDirs = []  //disable automatic ndk-build

    ext {
        targetABIList = ["x86", "armeabi", "armeabi-v7a"] as String[]
    }

    defaultConfig {
        applicationId "com.example.cuihao.zeellipse"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    task ndkBuild(type: Exec) {
        def t = String.join(" ", targetABIList)
        commandLine getNdkBuildCmd(), 'NDK_LIBS_OUT=jniLibs', 'APP_ABI=' + t, '-C', file('src/main').absolutePath
    }
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }
    splits {
        abi {
            enable true
            reset()
            include targetABIList
            universalApk false
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile project(':libs:opencv')
    compile 'com.commit451:PhotoView:1.2.4'
}
